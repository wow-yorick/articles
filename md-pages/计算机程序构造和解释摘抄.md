---
title: "计算机程序构造和解释摘抄"
date: 2023-07-06T13:47:41+08:00
lastmod: 2023-07-06T13:47:41+08:00
draft: false
tags: [""]
categories: ["人生"]
author: solid-10

# You can also close(false) or open(true) something for this content.
# P.S. comment can only be closed
comment: false
toc: false
autoCollapseToc: true
# You can also define another contentCopyright. e.g. contentCopyright: "This is another copyright."
contentCopyright: '<a href="https://github.com/wow-yorick/articles" rel="noopener" target="_blank">查看源</a>'
reward: false
mathjax: true
---

# 构造过程抽象

心智的活动，除了尽力产生各种简单的认识之外，主要表现在如下三个方面
 1. 将若干简单的认识**组合**为一个复合认识，由此产生出各种**复杂的认识**
 2. 将两个认识放在一起**对照**，不管它们如何简单或者复杂，在这样做时并不将它们合二为一。由此得到有关它们的**相互关系**
 3. 将**有关认识**与那些**在实际中**和它们**同在**的所有其他认识**隔离开**，这就是**抽象**，所有具有普遍性的认识都是这样得到的

 人们创建出一些称为程序的规则模式，以指导这类过程的进行。

 LISP具有许多独有的特征，这些特征使它成为**研究重要程序的设计、构造**，以及**各种数据结构**，**并**将其关联与支持它们的语言特征的一种及佳**媒介**

 LISP**最重要的特征**：计算过程的LISP描述本身又可以作为LISP的数据来表示和操作


## 1.1 程序设计的基本元素

### 1.1.1 表达式

描述一个语言时，需要将**注意力特别放在**这一语言所提供的,能够**将简单的认识组合起来形成复杂认识的方法方面**

每种强力语言都为此提供了三种机制

* **基本表达形式** 用于表示语言所关心的最简单的个体
* **组合的方法** 从简单的东西出发构造出复合的元素
* **抽象的方法** 为复合对象命名，并将它们当作单元操作

美观打印格式规则：令各个运算对象垂直对齐

### 1.1.2 命名和环境

需要一种通过名字去使用计算对象的方式:**变量**

实际上，构造一个复杂的程序，也就是为了去一步步地创建出越来越复杂的计算性对象

将值与符号关联，而后又能提取出这些值。解释器必须维护某种存储能力，以便保持有关的名字-值的对偶的轨迹，这种存储被称为环境


### 1.1.3 组合式的求值

把与过程性思维有关的各种问题隔离出来

- 要求值一个组合式，做下面的事情
1. 求值该组合式的各个子表达式
2. 将作为最左子表达式(运算符)的值的那个过程运用于相应的实际参数，所谓实际参数也就是其他子表达式(运算对象)的值

环境所扮演的角色就是用于确定表达式中各个符号的意义

对各种表达式的求值规则可以描述为一个简单的通用规则和一组针对不多的特殊形式的专门规则

### 1.1.4 复合过程

- 数和算数运算是基本的数据和过程
- 组合式的嵌套提供了一种组织起多个操作的方法
- 定义是一种受限的抽象手段，它为名字关联相应的值

### 1.1.5 过程应用的代换模型

- 代换的作用只是为了帮助我们领会过程调用中的情况，而不是对解释器实际工作方式的具体描述 （解释器在实际中，一般采用提供形式参数的局部环境的方式，产生"代换"的效果）
- 在模拟工程现象时，我们总是从最简单的不完全的模型开始。随着模型不在能满足更细致检查所考虑的问题，从而必须用进一步精化的模型取代

**运用序和正则序**
- 正则序求值: 完全展开而后归约
- 应用序求值: 先求值参数后应用

采用应用序可以避免对表达式的重复求值，从而提高一些效率，更重要的是，在超出了可以采用替换方式模拟的过程范围后，正则序的处理将变得更复杂的多

### 1.1.6 条件表达式和谓词
用术语*谓词*指哪些返回真假的*过程*，也指那种能求出真或者假的值的*表达式*

`and`和`or`都是**特殊形式** 而**不是**普通的过程，因为他们的子表达式不一定都求值， `not`是一个普通过程

### 1.1.7 实例: 采用牛顿法求平方根
数学函数与程序过程之间的矛盾,是描述一件事*特征*与描述*如何去做*这件事之间的普遍性差异的一个具体反映（明性的知识与行动性的知识之间的差异）

方法： 如果对x的平方根的值有了一个猜测y, 求出y和x/y的平均值（它更接近实际的平方根值）
立方根的近似值: 如果y是x的立方根的一个近似值，更好的一个近似值: x/y^2+2y/3

### 1.1.8 过程作为黑箱抽象

分解为若干子问题，每个过程完成一件可以清除标明的工作，使它们可以被用作定义其他过程的模块

一个过程定义应该能隐藏起一些细节

参数必须是所有过程体里局部的东西

## 1.2 过程与它们所产生的计算

基本算术操作 -> 对其进行组合 -> 定义各种复合过程 -> 对复合过程进行抽象

### 1.2.1 线性的递归和迭代
线性递归过程 (由解释器维持计算的过程)
迭代计算过程（其状态可以用固定数目的状态变量描述的计算过程）
 - 存在着一套固定的规则，一个描述计算过程在从一个状态到下一状态转换时，这些变量的更新方式；
 - 一个可能有的结束检测,描述这一计算过程应该终止的条件

 递归过程：论述的是语法形式上的事实：这个过程的定义中直接或间接地**引用该过程本身**
 递归计算过程：计算过程的进展方式

### 1.2.2 树形递归

树形递归计算过程里所需的步骤数将正比于树中的节点数，其空间需求正比于树的最大深度

在层次结构性的数据上操作，树形递归计算过程是一种自然的、威力强大的工具

例： 换零钱方式统计

### 1.2.3 增长的阶

增长的阶为对数时，当问题规模增大一倍时，所需资源量只增加一个常数

### 1.2.4 求幂

``` scheme
(define (fast-expt b n)
    (expt-iter b n 1))

(define (expt-iter b n a)
    (cond ((= n 0)
            a)
          ((even? n)
            (expt-iter (square b)
                       (/ n 2)
                       a))
          ((odd? n)
            (expt-iter b
                       (- n 1)
                       (* b a)))))
```

定义一个不变量，要求它在状态之间保持不变，这一技术是思考迭代算法设计问题时的一种非常强有力的方法

### 1.2.5 最大公约数


```
r = remainder(a/b)
GCD(a,b)=GCD(b,r)
```

### 1.2.6素数检查

1. 寻找因子
如果n不是素数，它必然有一个小于或等于根号nd的因子,意味着只需要在1和根号n之间检查因子

2. 费马检查
多次运行检查，如果测试的次数越多，就可以不断增加对有关结果的信心

3. 概率方法
存在着使出错机会达到任意*小*的检查算法。


## 1.3 用高阶函数做抽象

为公共的模式命名，建立抽象，而后直接在抽象的层次上工作
以过程作为参数，或以过程作为返回值，这类能`操作过程`的过程称为**高阶过程**

### 1.3.1 过程作为参数
### 1.3.2 用lambda构造过程
- let 使人能在尽可能接近其使用的地方建立局部变量约束
- 构成局部变量,不影响外层同名变量
### 1.3.3 过程作为一般性的方法

- 通过区间折半寻找方程的根
- 找出函数的不动点 :: 平均阻尼有助于计算快速收敛

### 1.3.4 过程作为返回值

选择过程的形式要清晰且易理解,使该计算过程中有用的元素能表现为一些相互分离的个体,并使它们还能重新用于其他的应用

`抽象和第一级过程`

高阶过程的重要性，就在于使我们能显式地用程序设计语言的要素去描述这些抽象，使我们能像操作其他计算元素一样去操作它们


# 构造数据抽象

复合数据同样是为了提升我们在设计程序时所位于的`概念层次`,提高设计的模块性，增强语言的表达能力

数据抽象技术能使程序更`容易`设计、维护和修改

复合数据对象能够成为以`混合`与`匹配`

数据导向的程序设计: 允许我们孤立的设计每一种数据表示，然后用添加的方式将它们组合进去(不需要任何修改)

## 2.1 数据抽象导引

过程的使用方式与该过程究竟如何通过更基本的过程实现的具体细节相互分离

`数据抽象的基本思想`,设法构造出一些使用复合数据对象的程序，使它们就像是在*抽象数据*上操作一样
    > 为每一类数据对象标识出一组操作，使得兑这类数据对象的的所有操作都可以基于它们表述，而且在操作这些数据对象时也只能使用它们

我们程序中使用数据的方式应该是这样的，除了完成当前工作所必要的东西之外，它们不对所用数据做任何多余的假设

一种'具体'数据表示的定义，也应该与程序中使用数据的方式无关.

### 2.1.1 实例：有理数的算术运算

### 2.1.2 抽象屏障

把对于具体表示方式的依赖性限制到少数几个界面过程，不但对修改程序有帮助，同时也有助于程序的设计,因为这种做法将使我们能保留考虑不同实现的灵活性

可以将过程作为对象去操作(数据的过程性表示将在我们的程序宝库里扮演一种核心角色)

## 2.2 层次性数据和闭包性质
组合起的数据对象得到的结果本身还可以通过同样的操作再进行组合

#### 2.2.1 序列的表示

`cons` `car` `cdr`

- 表操作
- 对表的映射
map 不仅因为它代表了一种公共模式，而且因为它建立起了一种处理表的高层抽象(强调的是从元素表到结果表的一个变换),不再把注意力吸引到对表中数据逐个处理的细节上

#### 2.2.2 层次性结构

对树的映射
通过序列操作和递归的组合实现
```lisp
(define (scale-tree tree factor)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (* tree factor))
        (else (cons (scale-tree (car tree) factor)
                    (scale-tree (cdr tree) factor)))))

(define (scale-tree tree factor)
  (map (lambda (sub-tree)
         (if (pair? sub-tree)
             (scale-tree sub-tree factor)
             (* sub-tree factor)))
       tree))

(define (tree-map f tree)
  (cond ((null? tree)
         '())
        ((not (pair? tree))
         (f tree))
        (else
         (cons (tree-map f (car tree))
               (tree-map f (cdr tree))))))

(define (tree-map2 f tree)
  (map (lambda (sub-tree)
         (if (pair? sub-tree)
             (tree-map2 f sub-tree)
             (f sub-tree)))
       tree))

(define (subsets s)
  (if (null? s)
      (list '())
      (let ((rest (subsets (cdr s))))
        (append rest (map (lambda (e)
                            (cons (car s) e)) rest)))))

(subsets '(1 2 3))
```

#### 2.2.3 序列作为一种约定的界面

序列操作

enumerate -> filter -> map -> accumulate

通过提供一个标准部件的库，并使这些部件都有着一些能以各种灵活方式相互连接的约定界面，将能进一步推动人们去做模块化的设计
模块化结构是控制复杂性的一种威力强大的策略

> 在真实的信号处理应用中，设计者通常总是从标准化的过滤器和变换装置族中选出一些东西，通过级联的方式构造出各种系统

嵌套映射
```lisp

(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))


(define (flatmap proc seq)
  (accumulate append '() (map proc seq)))

(define (permutations s)
  (if (null? s)
      '()
      (flatmap (lambda (x)
                 (map (lambda (p) (cons x p))
                      (permutations (remove x s))))
               s)))

(define (remove item sequence)
  (filter (lambda (x) (not (= x item)))
          sequence))

(define (map proc items)
  (if (null? items)
      '()
      (cons (proc (car items)
                  (map proc (cdr items))))))

(define (enumerate-interval2 n)
  (define (iter result n)
    (if (= n 0) result
        (iter (cons n result) (- n 1))))
  (iter '() n))
```

八皇后
```lisp
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))


(define (flatmap proc seq)
  (accumulate append '() (map proc seq)))


(define (queens board-size)
  (define (queen-cols k)
    (if (= k 0)
        (list empty-board)
        (filter
         (lambda (positions) (safe? k positions))
         (flatmap
          (lambda (rest-of-queens)
            (map (lambda (new-row)
                   (adjoin-position new-row k rest-of-queens))
                 (enumerate-interval 1 board-size)))
          (queen-cols (- k 1))))))
  (queen-cols board-size))

(define empty-board '())

(define (adjoin-position new-row k rest-of-queens)
  (cons new-row rest-of-queens))

(define (safe? k position)
  (iter-check (car position)
              (cdr position)
              1))

(define (iter-check row-of-new-queen rest-of-queens i)
  (if (null? rest-of-queens)
      #t
      (let ((row-of-current-queen (car rest-of-queens)))
        (if (or (= row-of-new-queen row-of-current-queen) ; 行碰撞
                (= row-of-new-queen (+ row-of-current-queen i)) ; 右下碰撞
                (= row-of-new-queen (- row-of-current-queen i))) ; 左下碰撞
            #f
            (iter-check row-of-new-queen
                        (cdr rest-of-queens) ; 继续检查剩余的皇后
                        (+ i 1)))))) ; 更新步进


(define (enumerate-interval i n)
  (define (iter result n)
    (if (= n 0) result
        (iter (cons n result) (- n 1))))
  (iter '() n))

```

图形语言
在描述一种语言时，应该将注意力集中到语言的基本原语、它的组合手段以及它的抽象手段，只是`最重要的`

我们所用的数据再组合方式下的闭包性质非常重要，因为这使我们能用不多几个操作`构造出各种复杂的结构`

```lisp
(define wave2
  (beside wave (flip-vert wave)))

(define wave4
  (below wave2 wave2))
; 有了组合之后，抽象出典型的组合模式
(define (flipped-pairs painter)
    (let ((painter2 (beside painter (flip-vert painter)))
        (below painter2 painter2))))

(define wave4 (flipped-pairs wave))

(define (right-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (right-split painter (- n 1))))
        (beside painter (below smaller smaller)))))


(define (corner-split painter n)
  (if (= n 0)
      painter
      (let ((up (up-split painter (- n 1)))
            (right (right-split painter (- n 1))))
        (let ((top-left (beside up up))
              (bottom-right (below right right))
              (corner (corner-split painter (- n 1))))
          (beside (below painter top-left)
                  (below bottom-right corner))))))

(define (up-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (right-split painter (- n 1))))
        (below painter (beside smaller smaller)))))
```
高阶操作

可以在高阶上工作，抽象出画家的各种组合操作的模式
(可以把画家操作看成是操控和描述这些元素的组合方法的元素)
```lisp
(define (square-of-four tl tr bl br)
  (lambda (painter)
    (let ((top (beside (tl painter) (tr painter)))
          (bottom (beside (bl painter) (br painter))))
      (below bottom top))))

(define (flipped-pairs painter)
  (let ((combine4 (square-of-four identity flip-vert
                                  identity flip-vert)))
    (combine4 painter)))

(define (square-limit painter n)
  (let ((combine4 (square-of-four flip-horiz identity
                                  rotate180 flip-vert)))
    (combine4 (corner-split painter n))))
```

框架
`基准向量`描述的是*框架基准点* **相对**于平面上某个*绝对基准点*的`偏移量`
`角向量`描述了*框架的角***相对**于*框架基准点*的`偏移量`
如果两个角向量正交，这个框架就是一个矩形，否则就是一个一般的平行四边形

```lisp
(define (frame-coord-map frame)
  (lambda (v)
    (add-vert
     (origin-frame frame)
     (add-vert (scale-vect (xcor-vect v)
                           (edge1-frame frame))
               (scale-vect (ycor-vect v)
                           (edge2-frame frame))))))

(define (make-vect xcor ycor)
  (list xcor ycor))

(define (xcor-vect v)
  (car v))

(define (ycor-vect v)
  (cadr v))

(define (add-vect vect another-vect)
    (make-vect (+ (xcor-vect vect)
                  (xcor-vect another-vect))
               (+ (ycor-vect vect)
                  (ycor-vect another-vect))))

(define (sub-vect vect another-vect)
    (make-vect (- (xcor-vect vect)
                  (xcor-vect another-vect))
               (- (ycor-vect vect)
                  (ycor-vect another-vect))))

(define (scale-vect factor vect)
    (make-vect (* factor (xcor-vect vect))
               (* factor (ycor-vect vect))))

(define (make-frame origin edge1 edge2)
    (list origin edge1 edge2))

(define (origin-frame f)
    (car f))

(define (edge1-frame f)
    (cadr f))

(define (edge2-frame f)
    (caddr f))

(define (make-frame origin edge1 edge2)
    (cons origin
          (cons edge1 edge2)))

(define (origin-frame f)
    (car f))

(define (edge1-frame f)
    (cadr f))

(define (edge2-frame f)
    (cddr f))
```
画家
```lisp
(define (segments->painter segment-list)
  (lambda (frame)
    (for-each
     (lambda (segment)
       (draw-line
        ((frame-coord-map frame) (start-segment segment))
        ((frame-coord-map frame) (end-segment segment))))
     segment-list)))

(define (make-segment start end)
    (list start end))

(define (start-segment s)
    (car s))

(define (end-segment s)
    (cadr s))
```
将画家表示为过程，就在这一图形语言中树立起一道强有力的抽象屏障。使我们可以创建和混用基于各种图形能力的各种类型的基本画家
> 任何过程只要能取一个框架作为参数，画出某些可以伸缩后适合这个框架的东西，它就可以作为一个画家

画家的变换和组合
特别注意： 这里的画家数据抽象，特别是将画家用过程表示，怎样使beside的实现变得如此简单。这里的beside过程完全不必了解作为其成分的各个画家的任何东西，它只需要知道这些画家能够在指定框架里画出一些东西就够了
```lisp
(define (segments->painter segment-list)
  (lambda (frame)
    (for-each
     (lambda (segment)
       (draw-line
        ((frame-coord-map frame) (start-segment segment))
        ((frame-coord-map frame) (end-segment segment))))
     segment-list)))

(define (transform-painter painter origin corner1 corner2)
  (lambda (frame)
    (let ((m (frame-coord-map frame)))
      (let ((new-origin (m origin)))
        (painter
         (make-frame new-origin
                     (sub-vect (m corner1) new-origin)
                     (sub-vect (m corner2) new-origin)))))))

(define (flip-vert painter)
  (transform-painter painter
                     (make-vect 0.0 1.0) ; new origin
                     (make-vect 1.0 1.0)  ; new end edge1
                     (make-vect 0.0 0.0))) ; new end of edge2

; 把自己的图像收缩到给定框架右上四分之一区域
(define (shrink-to-upper-right painter)
  (transform-painter painter
                     (make-vect 0.5 0.5)
                     (make-vect 1.0 0.5)
                     (make-vect 0.5 1.0)))

; 逆时针旋转90度
(define (rotate90 painter)
  (transform-painter painter
                     (make-vect 1.0 0.0)
                     (make-vect 1.0 1.0)
                     (make-vect 0.0 0.0)))

; 向中心收缩
(define (squash-inwards painter)
  (transform-painter painter
                     (make-vect 0.0 0.0)
                     (make-vect 0.65 0.35)
                     (make-vect 0.35 0.65)))

(define (beside painter1 painter2)
  (let ((split-point (make-vect 0.5, 0.0)))
    (let ((paint-left
           (transform-painter painter1
                              (make-vect 0.0 0.0)
                              split-point
                              (make-vect 0.0 1.0)))
          (paint-right
           (transform-painter painter2
                              split-point
                              (make-vect 1.0 0.0)
                              (make-vect 0.5 1.0))))
      (lambda (frame)
        (paint-left frame)
        (paint-right frame)))))
```
强健设计的语言层次

分层设计的问题：一个复杂的系统应该通过一系列的层次构造出来，为了描述这些层次，需要使用一系列的语言。
构造各个层次的方式，就是设法组合起作为这一层次中部件的各种基本元素，而这样构造出的部件又可以作为另一个层次里的基本元素。
在分层设计中，每个层次上所用的语言都提供了一些基本元素、组合手段，还有对该层次中的适当细节做抽象的手段

`在复杂系统的工程中广泛使用这种分层设计方法`
例如：在计算机工程里，电阻和晶体管被组合起来(用模拟电路的语言)，产生出一些部件，例如与门、或门等等；这些门电路又被作为数字电路设计的语言中的基本元素。将这类部件组合起来，构成了处理器、总线和存储系统，随即，又通过它们的组合构造出各种计算机，此时采用的是适合于描述计算机体系结构的语言。计算机的组合可以进一步构造成分布式系统，采用的是适合描述网络互联的语言。我们还可以这样做下去...

分层设计有助于使程序更加强健，使我们更有可能在给定范围发生一些小改变时，只需对程序做少量的修改
分层结构中的每个层次都为表述系统的特征提供了一套独特词汇，以及一套修改这一系统的方式

## 2.3 符号数据
将表和符号标记为应该作为数据对象看待，而不是作为应该求职的表达式

### 2.3.2 实例：符号求导
按愿望编程

1. 代数表达式的表示

2. 集合作为未排序的表 - 任何元素的出现都不超过一次

```lisp
(define (element-of-set? x set)
  (cond ((null? set) #f)
        ((equal? x (car set)) #t)
        (else (element-of-set? x (cdr set)))))

(define (adjoin-set x set)
  (if (element-of-set? x set)
      set
      (cons x set)))

(define (intersection-set set1 set2)
  (cond ((or (null? set1) (null? set2)) '())
        ((element-of-set? (car set1) set2)
         (cons (car set1)
               (intersection-set (cdr set1) set2)))
        (else (intersection-set (cdr set1) set2))))

(define (union-set set1 set2)
  (define (iter input result)
  (if (null? input)
      (reverse result)
      (let ((current-element (car input))
            (remain-element (cdr input)))
        (if (element-of-set? current-element result)
            (iter remain-element result)
            (iter remain-element
                  (cons current-element resule))))))

  (iter (append set1 set2) '()))


(define (intersection-set set another)
  (define (iter set result)
    (if (or (null? set) (null? another))
        (reverse result)
        (let ((current-element (car set))
              (remain-element (cdr set)))
          (if (and (element-of-set? current-element another)
                   (not (element-of-set? current-element result)))
              (iter remain-element
                    (cons current-element result))
              (iter remain-element result)))))
  (iter set '()))
```

3. 集合作为排序的表
4. 集合作为二叉树 p106
    节点左边的子树节点都小于它
    节点右边的子树节点都大于它
5. 集合与信息检索

Huffman编码： 可以表示为一棵二叉树，其中的树叶是被编码的符号。树中每个非叶节点代表一个集合，其中包含了这一节点之下的所有树叶上的符号。树叶的每个符号还被赋予一个权重，非叶节点所包含的权重是位于它之下的所有叶节点的权重之和

生成Huffman树
设法安排这棵树，使得那些带有最低频度的符号出现在离树根最远的地方
> 这一构造过程从叶节点的集合开始，这种节点中包含各个符号和它们的频度，这就是开始构造编码的初始数据。
```lisp
(define (make-leaf symbol weight)
  (list 'leaf symbol weight))

(define (leaf? object)
  (eq? (car object) 'leaf))

(define (symbol-leaf x) (cadr x))
(define (weight-leaf x) (caddr x))

(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))

(define (left-branch tree) (car tree))
(define (right-branch tree) (cadr tree))

(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))

(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))
```

解码过程
```lisp
(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
               (choose-branch (car bits) current-branch)))
          (if (leaf? next=branch)
              (cons (symbol-leaf next-branch)
                    (decode-1 (cdr bits) tree))
              (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))

(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
        ((= bit 1) (right-branch branch))
        (else (error "bad bit -- CHOOSE-BRANCH" bit))))

```

带权重元素的集合
```lisp
(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))

(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)
                               (cadr pair))
                    (make-leaf-set (cdr pairs))))))

(make-leaf-set '((a 4) (b 2) (c 1)))
```
编码
```lisp

(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) treee))))


(define (encode-symbol symbol tree)
  (cond ((leaf? tree)
         '())
        ((symbol-in-tree? symbol (left-branch tree))
         (cons 0 (encode-symbol symbol (left-branch tree))))
        ((symbol-in-tree? symbol (right-branch tree))
         (cons 1 (encode-symbol symbol (right-branch tree))))
        (else
         (error "This symbol not in tree:" symbol))))

(define (symbol-in-tree? given-symbol tree)
  (not (false?
        (find (lambda (s)
                (eq? s given-symbol))
              (symbols tree)))))
```

生成Huffman树
```lisp
(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))

(define (successive-merge ordered-set)
  (cond ((= 0 (length ordered-set)) '())
        ((= 1 (length ordered-set)) (car ordered-set))
        (else
         (let ((new-sub-tree (make-code-tree (car ordered-set)
                                             (cadr ordered-set)))
               (remained-ordered-set (cddr ordered-set)))
           (successive-merge (adjoin-set new-sub-tree remained-ordered-set))))))

(generate-huffman-tree '((A 4) (B 2) (C 1) (D 1)))
```

## 2.4 抽象数据的多重表示
抽象屏障 eg：选择函数 和构造函数

数据抽象屏障是控制复杂性的强有力工具,可以将设计一个大程序的任务，分割为一组可以分别处理的较小任务

需要有抽象屏障去隔离互不相同的设计选择,以便允许不同的设计选择在同一个程序里共存

类型标志，数据导向

数据抽象原理的建议是所有复数操作都应该可以使用，无论计算机所用的具体表示形式是什么
### 2.4.2 带标志数据

认识数据抽象的一种方式将其看做“最小允诺原则”
每个数据对象都以其类型作为标志，选择函数就能够在不同的数据上以一种通用的方式操作
每个选择函数的定义行为依赖于它操作其上的特定数据类型
在将数据对象从一个层次传到另一个层次的过程中，剥去和加上标志的规范方式可以成为一种重要的组织策略

### 2.4.3 数据导向的程序设计和可加性
查表 - 通过显式处理操作-类型表格的方式，管理程序中的各种通用型操作

消息传递
```lisp
(define (make-from-real-imag x y)
  (define (dispatch op)
    (cond ((eq? op 'real-part) x)
          ((eq? op 'imag-part) y)
          ((eq? op 'magnitude)
           (sqrt (+ (square x) (square y))))
          ((eq? op 'angle) (atan y x))
          (else
           (error "Unknown op -- MAKE-FROM-REAL-IMAG" op))))
  dispatch)
```
## 2.5 带有通用型操作的系统
### 2.5.1 通用型算数运算
### 2.5.2 不同类型数据的组合
- 强制
```lisp
(define (apply-generic op . args)
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (if (= (length args) 2)
              (let ((type1 (car type-tags))
                    (type2 (cadr type-tags))
                    (a1 (car args))
                    (a2 (cadr args)))
                (let ((t1->t2 (get-conercion type1 type2))
                      (t2->t1 (get-conercion type2 type1)))
                  (cond (t1->t2
                         (apply-generic op (t1->t2 a1) a2))
                        (t2->t1
                         (apply-generic op a1 (t2->t1 a2)))
                        (else
                         (error "No method for these types"
                                (list op type-tags)))))))
          (error "No method for these types"
                 (list op type-tags))))))
```
- 类型的层次结构
1. 塔结构，将一个新类型加入层次结构的问题可以极大的简化,每个类型都需要一个raise过程
2. 每个类型能够“继承”其超类型中定义的所有操作
3. 使我们有一种简单的方式去“下降”一个数据对象，使之达到最简单的表示形式

- 层次结构的不足
eg:几何图形类型间的关系

### 2.5.3 实例：符号代数
多项式算术
项表的表示

# 模块化、对象和状态
即使在变化中，它也丝毫未变
变得越多，它就越是原来的样子
使系统能够“自然地” 划分为一些具有内聚力的部分
对模拟真实物理系统的程序基于被模拟系统的结构去设计程序的结构。每个对象，构造起一个与之对应的计算对象；系统里的每种活动，在计算系统中定义一种符号操作
- 注意力集中在对象上- 基于环境模型与时间搏斗(处理并发任务变得困难)
- 注意力集中在流过系统的信息流上 - 松解在模型中对时间的模拟与计算机求值过程中的各种事件发生的顺序

## 3.1 赋值和局部状态

### 3.1.1 局部状态变量
### 3.1.2 引进赋值带来的利益
将系统看作是一集带有局部状态的对象，也是一种维护模块化设计的强有力技术
### 3.1.3 引进赋值的代价
不用任何赋值的程序设计称为*函数式程序设计*
代换模型的基数是：符号不过是作为值的名字
引入Set!后，变量索引着一个可以保存值的位置，存储在那里的值也是可以改变的

同一和变化
命令式程序设计的缺陷 -- 需要考虑计算的先后次序，因为引入赋值后，会产生副作用
## 3.2 求值的环境模型
一个环境就是框架的一个序列,每个框架是包含着一些约束的一个表格(可能为空),这些约束将一些变量名字关联与对应的值（一个框架里，任何变量至多只能有一个约束）。每个框架还包含着一个指针，指向这一框架的外围环境
如果讨论的框架是全局的，那它没有外围环境

### 3.2.1 求值规则

应该把步骤的特定顺序总看作一个实现细节，永远不要去写依赖于特定顺序的程序(一个复杂的编译器去做程序优化，它完全可能改变其中各个表达式的求值顺序)
将一个过程应用于一组实际参数时，将会建立起一个新环境，其中包含了将所有形式参数约束于对应的实际参数的框架，该框架的外围环境就是所用的那个过程的环境。随后就在这个新环境之下求值过程的体

### 3.2.2 简单过程的应用
通过创建不同的框架，维持所有名字为x的局部变量互不相同
由square创建的每个框架都指向全局环境,因为这就是对应于square的过程对象所指定的环境

### 3.2.3 将框架看作局部状态的展台
```lisp
(let ((<var> <exp>)) <body>)
;等价
((lambda (<var>) <body>) <exp>)
```

### 3.2.4 内部定义
